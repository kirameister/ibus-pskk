# justfile

# Variables
venv_path := "/opt/ibus-pskk/venv"
install_root := "/opt/ibus-pskk"
ibus_component_dir := "/usr/share/ibus/component"
gschema_dir := "/usr/share/glib-2.0/schemas"
icon_dir := "/usr/local/share/icons/hicolor"

# SKK Dictionary download settings
skk_dict_dir := "data/skk_dict"
skk_dict_base_url := "https://raw.githubusercontent.com/skk-dev/dict/master"
skk_dict_files := "SKK-JISYO.L SKK-JISYO.M SKK-JISYO.ML SKK-JISYO.S"

# i18n settings
locale_dir := "locales"
domain := "ibus-pskk"
locale_install_dir := "/usr/share/locale"


# Default recipe (runs when you just type 'just')
default:
    @just --list

# Create virtual environment (skip if already exists)
create-venv:
    @if [ ! -d "{{venv_path}}" ]; then \
        echo "Creating venv at {{venv_path}}..."; \
        python3 -m venv --system-site-packages {{venv_path}}; \
    else \
        echo "venv already exists at {{venv_path}}, skipping"; \
    fi

# Install Python dependencies
install-deps: create-venv
    {{venv_path}}/bin/pip install -r requirements.txt

# Compile and install GSettings schema
install-schema:
    mkdir -p {{gschema_dir}}
    cp data/org.freedesktop.ibus.engine.pskk.gschema.xml {{gschema_dir}}/
    glib-compile-schemas {{gschema_dir}}

# Install ICON
install-icons:
    mkdir -p {{icon_dir}}/scalable/apps
    cp data/icons/ibus-pskk.svg {{icon_dir}}/scalable/apps/
    chmod 644 {{icon_dir}}/scalable/apps/ibus-pskk.svg
    gtk-update-icon-cache {{icon_dir}} || true

# Install IME files and data
install-files:
    mkdir -p {{install_root}}/lib
    cp -r src/* {{install_root}}/lib/
    # Generate paths.py with installation paths
    echo '# Auto-generated during installation - do not edit manually' > {{install_root}}/lib/paths.py
    echo 'INSTALL_ROOT = "{{install_root}}"' >> {{install_root}}/lib/paths.py
    echo 'VENV_PATH = "{{venv_path}}"' >> {{install_root}}/lib/paths.py
    echo 'IBUS_COMPONENT_DIR = "{{ibus_component_dir}}"' >> {{install_root}}/lib/paths.py
    echo 'GSCHEMA_DIR = "{{gschema_dir}}"' >> {{install_root}}/lib/paths.py
    echo 'ICON_DIR = "{{icon_dir}}"' >> {{install_root}}/lib/paths.py
    # Copy default config file
    cp data/default_user_config.json {{install_root}}/config.json
    chmod 644 {{install_root}}/config.json
    # Copy layout files
    mkdir -p {{install_root}}/layouts
    cp data/layouts/* {{install_root}}/layouts/
    mkdir -p {{install_root}}/kanchoku_layouts
    cp data/kanchoku_layouts/* {{install_root}}/kanchoku_layouts/
    # Copy skk_dict files
    mkdir -p {{install_root}}/dictionaries
    mkdir -p {{install_root}}/dictionaries/skk_dict
    cp data/skk_dict/* {{install_root}}/dictionaries/skk_dict/
    # Copy wagahai-ha-nekodearu txt file
    mkdir -p {{install_root}}/data
    cp data/crf_training/*.txt {{install_root}}/data/
    # XML file copy
    cp data/pskk.xml {{ibus_component_dir}}/
    chmod 644 {{ibus_component_dir}}/pskk.xml

# Install locale files (.mo) to system directory
install-locales:
    #!/usr/bin/env bash
    set -euo pipefail
    for mo_file in {{locale_dir}}/*/LC_MESSAGES/{{domain}}.mo; do
        if [ -f "$mo_file" ]; then
            # Extract language code from path (e.g., "ja" from "locales/ja/LC_MESSAGES/...")
            lang=$(basename $(dirname $(dirname "$mo_file")))
            mkdir -p {{locale_install_dir}}/$lang/LC_MESSAGES
            cp "$mo_file" {{locale_install_dir}}/$lang/LC_MESSAGES/{{domain}}.mo
            echo "Installed $lang locale"
        fi
    done

# Full installation
install: install-deps download-skk-dicts i18n-compile install-files install-locales install-schema install-icons setup-user-config
    chmod 755 {{install_root}}/lib/*
    @echo "Installation complete!"
    @echo "Run 'ibus restart' to activate the IME"

# Generate paths.py for development (in src/)
generate-paths:
    @echo '# Auto-generated by justfile - do not edit manually' > src/paths.py
    @echo 'INSTALL_ROOT = "{{install_root}}"' >> src/paths.py
    @echo 'VENV_PATH = "{{venv_path}}"' >> src/paths.py
    @echo 'IBUS_COMPONENT_DIR = "{{ibus_component_dir}}"' >> src/paths.py
    @echo 'GSCHEMA_DIR = "{{gschema_dir}}"' >> src/paths.py
    @echo 'ICON_DIR = "{{icon_dir}}"' >> src/paths.py
    @echo "Generated src/paths.py for development"

# Development installation (uses local venv)
dev-install: generate-paths
    python3 -m venv venv
    ./venv/bin/pip install -r requirements.txt
    #./venv/bin/pip install -e .

# Uninstall
uninstall:
    rm -rf {{install_root}}
    rm -f {{ibus_component_dir}}/pskk.xml
    rm -f {{gschema_dir}}/org.freedesktop.ibus.engine.pskk.gschema.xml
    glib-compile-schemas {{gschema_dir}}
    rm -f {{icon_dir}}/scalable/apps/ibus-pskk.svg
    gtk-update-icon-cache {{icon_dir}} || true
    # Remove locale files
    rm -f {{locale_install_dir}}/*/LC_MESSAGES/{{domain}}.mo
    @echo "Uninstalled. Run 'ibus restart'"

# Restart IBus
restart-ibus:
    ibus restart

# Clean development files
clean:
    rm -rf venv
    rm -rf build dist *.egg-info
    rm -f src/paths.py
    find . -type d -name __pycache__ -exec rm -rf {} +

# Download SKK dictionaries from GitHub (skips if files already exist)
# Downloads EUC-JP encoded files and converts them to UTF-8
download-skk-dicts:
    #!/usr/bin/env bash
    set -euo pipefail
    mkdir -p {{skk_dict_dir}}
    for dict_file in {{skk_dict_files}}; do
        target_path="{{skk_dict_dir}}/${dict_file}"
        if [ -f "$target_path" ]; then
            echo "✓ ${dict_file} already exists, skipping"
        else
            echo "↓ Downloading ${dict_file}..."
            # Download to temporary file first
            tmp_file=$(mktemp)
            curl -fSL "{{skk_dict_base_url}}/${dict_file}" -o "$tmp_file"
            # Convert from EUC-JP to UTF-8
            echo "  Converting EUC-JP → UTF-8..."
            iconv -f EUC-JP -t UTF-8 "$tmp_file" > "$target_path"
            rm -f "$tmp_file"
            echo "  Saved to ${target_path}"
        fi
    done
    echo "SKK dictionary download complete!"

# Force re-download SKK dictionaries (removes existing files first)
download-skk-dicts-force:
    rm -f {{skk_dict_dir}}/SKK-JISYO.*
    just download-skk-dicts

# Run tests
test:
    ./venv/bin/pytest tests/

# ============================================================================
# i18n (Internationalization) recipes
# ============================================================================

# Extract translatable strings from Python source files to .pot template
i18n-extract:
    #!/usr/bin/env bash
    set -euo pipefail
    mkdir -p {{locale_dir}}
    xgettext \
        --language=Python \
        --keyword=_ \
        --keyword=N_ \
        --output={{locale_dir}}/{{domain}}.pot \
        --package-name={{domain}} \
        --copyright-holder="ibus-pskk contributors" \
        --msgid-bugs-address="" \
        --from-code=UTF-8 \
        --add-comments=TRANSLATORS: \
        src/*.py
    echo "Extracted strings to {{locale_dir}}/{{domain}}.pot"

# Initialize a new language translation file (usage: just i18n-init ja)
i18n-init lang:
    #!/usr/bin/env bash
    set -euo pipefail
    if [ ! -f "{{locale_dir}}/{{domain}}.pot" ]; then
        echo "Error: Template file {{locale_dir}}/{{domain}}.pot not found."
        echo "Run 'just i18n-extract' first."
        exit 1
    fi
    mkdir -p {{locale_dir}}/{{lang}}/LC_MESSAGES
    if [ -f "{{locale_dir}}/{{lang}}/LC_MESSAGES/{{domain}}.po" ]; then
        echo "Warning: {{locale_dir}}/{{lang}}/LC_MESSAGES/{{domain}}.po already exists."
        echo "Use 'just i18n-update' to merge new strings instead."
        exit 1
    fi
    msginit \
        --input={{locale_dir}}/{{domain}}.pot \
        --output={{locale_dir}}/{{lang}}/LC_MESSAGES/{{domain}}.po \
        --locale={{lang}}
    echo "Created {{locale_dir}}/{{lang}}/LC_MESSAGES/{{domain}}.po"

# Update existing .po files with new strings from .pot template
i18n-update:
    #!/usr/bin/env bash
    set -euo pipefail
    if [ ! -f "{{locale_dir}}/{{domain}}.pot" ]; then
        echo "Error: Template file {{locale_dir}}/{{domain}}.pot not found."
        echo "Run 'just i18n-extract' first."
        exit 1
    fi
    for po_file in {{locale_dir}}/*/LC_MESSAGES/{{domain}}.po; do
        if [ -f "$po_file" ]; then
            echo "Updating $po_file..."
            msgmerge --update --backup=none "$po_file" {{locale_dir}}/{{domain}}.pot
        fi
    done
    echo "Updated all .po files"

# Compile all .po files to .mo binary files
i18n-compile:
    #!/usr/bin/env bash
    set -euo pipefail
    for po_file in {{locale_dir}}/*/LC_MESSAGES/{{domain}}.po; do
        if [ -f "$po_file" ]; then
            mo_file="${po_file%.po}.mo"
            echo "Compiling $po_file → $mo_file"
            msgfmt --output="$mo_file" "$po_file"
        fi
    done
    echo "Compiled all .mo files"

# Full i18n workflow: extract → update → compile
i18n: i18n-extract i18n-update i18n-compile
    @echo "i18n workflow complete"

# Setup user config directory in $HOME/.config/ibus-pskk/
setup-user-config:
    #!/usr/bin/env bash
    set -euo pipefail
    # Determine the actual user's home directory (handles sudo case)
    if [ -n "${SUDO_USER:-}" ]; then \
        USER_HOME=$(getent passwd "$SUDO_USER" | cut -d: -f6); \
        ACTUAL_USER="$SUDO_USER"; \
    else \
        USER_HOME="$HOME"; \
        ACTUAL_USER="$USER"; \
    fi
    echo "Setting up user config directory for user: $ACTUAL_USER"
    USER_CONFIG_DIR="$USER_HOME/.config/ibus-pskk"
    # Create directories
    mkdir -p "$USER_CONFIG_DIR/layouts"
    mkdir -p "$USER_CONFIG_DIR/kanchoku_layouts"
    mkdir -p "$USER_CONFIG_DIR/dictionaries"
    # Copy default config if not exists
    if [ ! -f "$USER_CONFIG_DIR/config.json" ]; then \
        cp data/default_user_config.json "$USER_CONFIG_DIR/config.json"; \
        echo "Copied default config to $USER_CONFIG_DIR/config.json"; \
    else \
        echo "Config file already exists at $USER_CONFIG_DIR/config.json (skipping)"; \
    fi
    # Copy layout files (always overwrite to get latest versions)
    cp data/layouts/*.json "$USER_CONFIG_DIR/layouts/"
    cp data/kanchoku_layouts/*.json "$USER_CONFIG_DIR/kanchoku_layouts/"
    # Copy the trained CRF file (only if not already present, to preserve user-trained models)
    if [ ! -f "$USER_CONFIG_DIR/bunsetsu.crfsuite" ]; then \
        cp data/crf_training/bunsetsu.crfsuite "$USER_CONFIG_DIR/"; \
        echo "Installed default CRF model"; \
    else \
        echo "CRF model already exists, skipping (use 'just update-crf-model' to force update)"; \
    fi
    # Create an empty JSON file called system_dictionary.json
    if [ ! -f "$USER_CONFIG_DIR/system_dictionary.json" ]; then \
        echo '{}' > $USER_CONFIG_DIR/system_dictionary.json
        echo "Created empty system_dictionary.json"; \
    else \
        echo "system_dictionary.json already exists, skipping"; \
    fi
    # Create an empty JSON file called imported_user_dictionary.json
    if [ ! -f "$USER_CONFIG_DIR/imported_user_dictionary.json" ]; then \
        echo '{}' > $USER_CONFIG_DIR/imported_user_dictionary.json
        echo "Created empty imported_user_dictionary.json"; \
    else \
        echo "imported_user_dictionary.json already exists, skipping"; \
    fi
    # Create an empty JSON file called user_dictionary.json
    if [ ! -f "$USER_CONFIG_DIR/user_dictionary.json" ]; then \
        echo '{}' > $USER_CONFIG_DIR/user_dictionary.json
        echo "Created empty user_dictionary.json"; \
    else \
        echo "user_dictionary.json already exists, skipping"; \
    fi
    # Fix ownership if run with sudo
    if [ -n "${SUDO_USER:-}" ]; then \
        chown -R "$SUDO_USER:$SUDO_USER" "$USER_CONFIG_DIR"; \
        echo "Set ownership to $SUDO_USER"; \
    fi
    echo "User config directory setup complete at $USER_CONFIG_DIR"

## Project Structure
###
### ibus-pskk/
### ├── justfile                     # Build and installation automation
### ├── requirements.txt              # Python dependencies
### ├── LICENSE
### ├── README.md
### ├── src/                          # Source code
### │   ├── main.py                   # IBus engine entry point
### │   ├── engine.py                 # Main engine implementation
### │   ├── util.py                   # Utility functions (config, paths)
### │   ├── settings_panel.py         # GTK settings UI
### │   ├── conversion_model.py      # Conversion model panel (Test/Train)
### │   └── paths.py                  # Auto-generated installation paths
### ├── data/                         # Installation data
### │   ├── pskk.xml                  # IBus component definition
### │   ├── default_user_config.json # Default configuration
### │   ├── org.freedesktop.ibus.engine.pskk.gschema.xml  # GSettings schema
### │   ├── icons/
### │   │   └── ibus-pskk.svg        # Application icon
### │   └── layouts/                   # Kanchoku layout files
### │       ├── shingeta.json
### │       └── aki_code.json
### └── tests/                        # Unit tests
###     └── test_util.py              # Tests for util.py functions
###
### Installation Locations:
### - System files: /opt/ibus-pskk/
### - User config: ~/.config/ibus-pskk/
### - IBus component: /usr/share/ibus/component/
### - GSettings schema: /usr/share/glib-2.0/schemas/
### - Icons: /usr/local/share/icons/hicolor/
